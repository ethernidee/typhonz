ZVSE

; UN:Z CHANGES

; Universal Creature Upgrades
!?FU3301;
!!VRy1:S-1;  !!VRy2:S-1;
!!VRy1&x2>=115/x2<=121:Sx2 -115; [Garrison creature slot number: y1]
!!VRy2&x2>=140/x2<=146:Sx2 -140; [Visiting hero creature slot number: y2]
!!CA-1:H0/?y3 H1/?y4; [Garrison hero: y3, Visiting hero: y4]
!!HEy3&y3>=0/y1>=0:C0/y1/?y5/d; [Garrison hero creature: y5]
!!CA-1&y3<0/y1>=0:M2/y1/?y5/d; [Garrison creature: y5]
!!HEy4&y4>=0/y2>=0:C0/y2/?y5/d; [Visiting hero creature: y5]
!!FU&y5<0:E; [Exit if not a creature]
!!MA:Uy5/?y7; [Check for a custom upgrade: y7]
!!VRy6&y7=-1/y5<=111:Sy5 %2; [Remainder of creature number divided by 2: y6]
!!VRy7&y7=-1/y5<=111/y6=0:Sy5 +1; [If non-upgraded, upgrade is y7]
!!VRy7&y7=-1/y5=112:S127; [Air Elemental]
!!VRy7&y7=-1/y5=113:S125; [Earth Elemental]
!!VRy7&y7=-1/y5=114:S129; [Fire Elemental]
!!VRy7&y7=-1/y5=115:S123; [Water Elemental]
!!VRy7&y7=-1/y5=118:S119; [Pixie]
!!VRy7&y7=-1/y5=120:S121; [Psychic Elemental]
!!VRy7&y7=-1/y5=130:S131; [Firebird]
!!VRy6:S1; [Initialize y6 to 1]
!!VRy6&y7>=0:S0; [Set y6 to 0 if y7>=0]
!!FU|y6=1/y7=-2:E; [Exit if no upgrade]
!!MA:Ly5/?v3302; [Store creature's level in v3302]
!!VRy8:S37 +v3302; [Upgraded dwelling of this creature's level: y8]
!!CA-1:B3/y8; [Check if town has this upgraded dwelling built: Flag 1 True if yes]
!!CA-1&-1:B3/19; [Check if horde building 1 is built: Flag 1 is true if yes]
!!CA-1&-1:B3/25; [Check if horde building 2 is built: Flag 1 is true if yes]
!!FU&-1:E; [Exit if upgraded dwelling isn't built]
!!VRv3300:Sy5; [Store creature number: v3300]
!!MA:Oy5/?v3301;  [Store creature's town type in v3301]
!!MA:Oy5/x1; [Change creature to this town type]
!!VRv3310:Sy7; [Store upgraded creature number: v3310]
!!MA:Oy7/?v3311;  [Store upgraded creature's town type in v3311]
!!MA:Ly7/?v3312; [Store upgraded creature's level in v3312]
!!MA:Oy7/x1; [Change upgraded creature to this town type]
!!CA-1:P?y13/?y14/?y15; [Town's position: y13/y14/y15]
!!POy13/y14/y15:B0/?y16 B1/?y17 O?y18; [PO:B0: y16  PO:B1: y17  PO:O: y18]
!!VRy9:Sy18 -1; [Subtract 1 from y18 to get real creature level: y9]
!!UN&v3302<>y9:Zx1/-1/v3302/0/?y18; [If this level not stored, get non-upgraded type: y18]
!!UN&v3302<>y9:Zx1/-1/v3302/1/?y19; [If this level not stored, get upgraded type: y19]
!!VRy10&v3302<>y9:Sy18 +1; [Non-upgraded type +1: y10]
!!VRy11&v3302<>y9:Sy19 +1; [Upgraded type +1: y11]
!!VRy12&v3302<>y9:Sv3302 +1; [Creature level +1: y12]
!!POy13/y14/y15&v3302<>y9:B0/y10; [If this level not stored, store non-upgraded type (+1)]
!!POy13/y14/y15&v3302<>y9:B1/y11; [If this level not stored, store upgraded type (+1)]
!!POy13/y14/y15&v3302<>y9:Oy12; [If this level not stored, store creature level (+1)]
!!UN:Zx1/-1/v3302/0/y5; [Set non-upgraded creature]
!!UN:Zx1/-1/v3302/1/y7; [Set upgraded creature]

!?FU3302;
!!CA-1:P?y1/?y2/?y3; [Town's position: y1/y2/y3]
!!POy1/y2/y3:B0/?y4 B1/?y5 O?y6; [PO:B0: y4  PO:B1: y5  PO:O: y6]
!!VRy4&y4>0:-1; [Subtract 1 from y4 to get non-upgraded creature number]
!!VRy5&y5>0:-1; [Subtract 1 from y5 to get upgraded creature number]
!!VRy6&y6>0:-1; [Subtract 1 from y6 to get creature level]
!!UN&y4>=0:Zx1/-1/y6/0/y4; [Reset non-upgraded]
!!UN&y5>=0:Zx1/-1/y6/1/y5; [Reset upgraded]
!!FU&v3300<0:E; [Exit function if no creature changed]
!!MA:Ov3300/v3301;  [Restore creature's town type]
!!MA:Ov3310/v3311;  [Restore upgraded creature's town type]
!!VRv3300:S-1;
